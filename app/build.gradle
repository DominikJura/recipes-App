apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    maven { url 'https://maven.google.com/' }
}


configurations {
    ktlint
}

kapt {
    generateStubs = true
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "pl.jurassic.godtrecipes"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode Integer.parseInt(version_code)
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    flavorDimensions "mode"

    productFlavors {
        develop {
            dimension "mode"
            applicationIdSuffix ".develop"
            versionNameSuffix "-develop"
            buildConfigField 'String', 'BASE_URL', GODT_BASE_URL
            resValue 'string', 'app_name', 'GodtRecipes-develop'
        }
        production {
            dimension "mode"
            buildConfigField 'String', 'BASE_URL', GODT_BASE_URL
            resValue 'string', 'app_name', 'GodtRecipes'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.res.srcDirs += [
                'src/main/res/layouts',
                'src/main/res/layouts/activity',
                'src/main/res/layouts/fragment',
        ]
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //Espresso
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //JUnit
    testImplementation "junit:junit:4.12"
    //Mockito
    testImplementation "org.mockito:mockito-core:2.13.0"
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    //Ktlint
    ktlint "com.github.shyiko:ktlint:0.15.0"
    //Support
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:support-vector-drawable:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:support-annotations:$support_version"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"
    //ButterKnife
    implementation "com.jakewharton:butterknife:$butterknife_version"
    kapt "com.jakewharton:butterknife-compiler:$butterknife_version"
    //Dagger
    implementation "com.google.dagger:dagger:$dagger2_version"
    kapt "com.google.dagger:dagger-compiler:$dagger2_version"
    implementation "com.google.dagger:dagger-android:$dagger2_version"
    implementation "com.google.dagger:dagger-android-support:$dagger2_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger2_version"
    //RxAndroid
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "io.reactivex.rxjava2:rxjava:2.1.8"
    //Timber
    implementation "com.jakewharton.timber:timber:4.5.1"
    //Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.5.4"
    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    debugImplementation "com.squareup.okhttp3:logging-interceptor:3.9.1"
    //Room
    implementation "android.arch.persistence.room:runtime:1.0.0"
    implementation "android.arch.persistence.room:rxjava2:1.0.0"
    kapt "android.arch.persistence.room:compiler:1.0.0"
    //Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
